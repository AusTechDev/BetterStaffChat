/*
 * BetterStaffChat - build.gradle
 * Copyright (C) 2021 AusTech Development Team
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id "io.freefair.lombok" version "6.0.0-m2"
    id 'net.kyori.indra.checkstyle' version "1.2.1" apply false
}

group = 'dev.austech'
version = '1.1.0-SNAPSHOT'
var dev = true

// https://github.com/EssentialsX/Essentials/blob/2.x/build.gradle
ext {
    GIT_COMMIT = grgit == null ? "unknown" : grgit.head().abbreviatedId
    GIT_BRANCH = grgit == null ? "detached-head" : grgit.branch.current().name

    fullVersion = dev ? "-dev-${GIT_COMMIT}" : ""
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: "io.freefair.lombok"

    group = project.group
    version = project.version

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        maven { url 'https://m2.dv8tion.net/releases' }
    }

    dependencies {
        compileOnly 'net.dv8tion:JDA:4.2.1_262'
        compileOnly 'org.projectlombok:lombok:1.18.20'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':BetterStaffChat-common', configuration: 'shadow')
    implementation project(path: ':BetterStaffChat-spigot', configuration: 'shadow')
    implementation project(path: ':BetterStaffChat-bungeecord', configuration: 'shadow')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'net.dv8tion.jda', 'dev.austech.betterstaffchat.shaded.jda'
    relocate'org.bstats', 'dev.austech.betterstaffchat.shaded.metrics'
}

task copyJars(type: Copy) {
    outputTasks().forEach({ from(it) })
    rename '(.*)-all.jar', ('$1' + fullVersion + '.jar')
    into file('jars')
}

def outputTasks() {
    ["shadowJar", ":BetterStaffChat-spigot:shadowJar", ":BetterStaffChat-bungeecord:shadowJar"].stream().map({ tasks.findByPath(it) })
}

task cleanJars() {
    delete file('jars')
}

clean.dependsOn cleanJars
build.dependsOn shadowJar
build.dependsOn copyJars